apply plugin: 'com.android.application'

android {
    defaultConfig {
        applicationId "com.forrestguice.suntimes.naturalhour"
        minSdkVersion 14
        //noinspection OldTargetApi
        targetSdkVersion 33
        compileSdk 33

        versionCode 8
        versionName "0.3.2"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        manifestPlaceholders.suntimesApplicationId = "com.forrestguice.suntimeswidget"
        manifestPlaceholders.suntimesPermissionRoot = "suntimes"
        def authorityRoot = "suntimes.naturalhour"
        manifestPlaceholders.naturalHourAuthorityRoot = "$authorityRoot"
        buildConfigField "String", "AUTHORITY_ROOT", "\"$authorityRoot\""

        missingDimensionStrategy 'version', 'androidx'
        missingDimensionStrategy 'api', 'api33'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = [ "versiontype" ]
    productFlavors {
        production {
            dimension "versiontype"
            getIsDefault().set(true)
        }
        nightly {
            dimension "versiontype"
            applicationIdSuffix ".nightly"
            versionNameSuffix "-nightly"
            def nightlyVersionCode = new Date().format('yyyyMMddhh') as int
            versionCode nightlyVersionCode

            def authorityRoot = "suntimes.naturalhour.nightly"
            manifestPlaceholders.suntimesApplicationId = "com.forrestguice.suntimeswidget"
            manifestPlaceholders.suntimesPermissionRoot = "suntimes"
            manifestPlaceholders.naturalHourAuthorityRoot = "$authorityRoot"
            buildConfigField "String", "AUTHORITY_ROOT", "\"$authorityRoot\""
        }
    }

    lintOptions {
        disable 'MissingTranslation'//,'WrongRegion','ObsoleteSdkInt'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation project(path: ":SuntimesAddon")                           //implementation 'com.github.forrestguice:SuntimesAddon:v0.4.3'
    implementation 'androidx.appcompat:appcompat:1.6.1'                      // 1.7.0 (2024-05-29) requires Java 17
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'        // 2.2.0 (2024-10-30) requires minapi 21+
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'      // 1.3.0 (2025-02-26) requires minapi 21+
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.5.0'             // 2.7.0+ requires Java 17
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.5.0'            // 2.7.0+ requires Java 17
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'            // 1.2.0 requires Java 17
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.preference:preference:1.2.1"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:core:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

def getGitHash()
{
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}